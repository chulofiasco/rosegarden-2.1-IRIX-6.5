#!/usr/local/bin/tclmidi
#
# mmap.mt32 - a General MIDI -> Roland MT-32 remapping filter
#
# Copyright (c) 1994 by Luchezar Georgiev, Bulgaria <lucho@midi.tu-varna.bg>
#
# Tip: Run your GM files through this mapper in advance, put the resulting
# files in a ZIP or other archive, and then use `playzip' or similar shell
# script to play them from the archive. You'll save both disk space and
# playing time!
#
# It shouldn't be difficult for you to customize this to your synth. Try it!

if {$argc} {
    puts stderr {Usage: mmap.mt32 < GMfile.mid > MT-32file.mid}
    exit 1
}

# Channel map
set cm {1 2 3 4 5 6 7 8 8 9 10 11 12 13 14 15}

# Drum channel for GM ----^
set dchan 9

# Drum map
set dm	   { 0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 \
24 25 26 27 28 29 30 31 32 33 34 35 36 37 38 39 40 41 42 43 44 45 46 47 48 \
49 50 51 49 51 54 46 56 49 73 51 60 61 62 63 64 65 66 67 68 69 70 71 72 70 \
69 75 75 75 62 63 68 67 82 83 84 85 86 87 88 89 90 91 92 93 94 95 96 97 98 \
99 100 101 102 103 104 105 106 107 108 109 110 111 112 113 114 115 116 117 \
118 119 120 121 122 123 124 125 126 127 }

# Patch map
set pm	   { 0 1 3 7 5 6 17 21 22 101 101 97 104 103 102 105 12 9 10 13 14 \
15 87 15 59 60 59 62 61 59 62 62 64 67 66 71 68 69 66 70 53 52 54 56 53 51 \
57 112 48 50 48 50 34 42 33  122 88 90 94 89 92 95 89 91 78 79 80 81 84 85 \
86 83 75 73 76 77 110 107 108 72 47 67 75 51 61 72 52 67 32 33 67 34 32 32 \
33 33 41 36 35 37 45 33 43 32 63 105 105 105 51 81 52 81 23 103 103 113 117\
113 116 119 124 120 119 124 123 120 119 114 }

# Pitch wheel ratio. MT-32 sensitivity is 12 semitones, while GM - 2
set pwratio [expr 12/2]

set imf [midiread stdin]
midirewind $imf

set config [midiconfig $imf]
set format [lindex $config 0]
set division [lindex $config 1]
set tracks [lindex $config 2]

set omf [midimake]
midiconfig $omf $format $division $tracks

set errMsg ""

for {set i 0} {$i < [lindex $tracks 1]} {incr i} {
    while {[set event [midiget $imf $i next]] != "EOT"} {
	switch [set type [lindex $event 1]] {
	    "NoteOff" -
	    "NoteOn" -
	    "Note" {
		if {[lindex $event 2] == $dchan} {
		    # new channel and drum note
		    set event [lreplace $event 2 3 \
		      [lindex $cm [lindex $event 2]] \
		      [lindex $dm [lindex $event 3]]]
		} else {
		    # new channel only
		    set event [lreplace $event 2 2 \
		      [lindex $cm [lindex $event 2]]]
		}
	    }
	    "KeyPressure" -
	    "Parameter" -
	    "ChannelPressure" {
		# new channel
		set event [lreplace $event 2 2 [lindex $cm [lindex $event 2]]]
	    }
	    "Program" {
		# new channel and program
		set event [lreplace $event 2 3 [lindex $cm [lindex $event 2]]\
		  [lindex $pm [lindex $event 3]]]
	    }
	    "PitchWheel" {
		# new pitchwheel value
		set pw [expr ([lindex $event 3] - 8192) / $pwratio + 8192]
		set event [lreplace $event 2 3 [lindex $cm [lindex $event 2]]\
	          $pw]
	    }
	}
	# make it robust
	catch {
	    midiput $omf $i $event
	} errMsg
	if {$errMsg != ""} {
	    puts stderr "$errMsg \"$event\""
	}
    }
}

midiwrite stdout $omf
midifree $imf
midifree $omf
