
dnl Process this file with autoconf to produce a configure script.
AC_INIT(topbox/src/Main.c)

AC_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB
dnl Checks for makedepend, uses /bin/true if not found
AC_PATH_PROG(MAKEDEPEND, makedepend, /bin/true)

AC_CANONICAL_SYSTEM

AC_CHECKING(X-related files and stuff)
AC_PATH_XTRA

dnl Replace `main' with a function in -lSM:
dnl AC_CHECK_LIB(SM, main)
dnl Replace `main' with a function in -lX11:
dnl AC_CHECK_LIB(X11, main)
dnl Replace `main' with a function in -lXaw:
dnl Replace `main' with a function in -lXext:
dnl AC_CHECK_LIB(Xext, main)
dnl Replace `main' with a function in -lXmu:
dnl AC_CHECK_LIB(Xmu, main)
dnl Replace `main' with a function in -lXt:
dnl AC_CHECK_LIB(Xt, main)
dnl Replace `main' with a function in -lmalloc:
AC_CHECK_LIB(malloc, mallopt, AC_DEFINE(HAVE_SPECIALIST_MALLOC_LIBRARY))

dnl Checks for header files.
dnl AC_HEADER_DIRENT
AC_CHECKING(Standard C stuff)
AC_HEADER_STDC
dnl AC_HEADER_SYS_WAIT
dnl AC_CHECK_HEADERS(fcntl.h limits.h sys/ioctl.h sys/time.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T

dnl regexp_lib="-L\$(REGEXP)/lib -lRegexp"

dnl Checks for library functions.
dnl AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
dnl AC_CHECK_FUNCS(getcwd gethostname regcomp strcspn strdup strspn strstr)
AC_CHECK_FUNC(gethostname, , AC_DEFINE(NO_GETHOSTNAME))
AC_CHECK_FUNC(strcasecmp, , AC_DEFINE(NO_STRCASECMP))
dnl AC_CHECK_FUNC(regexec, AC_DEFINE(USE_POSIX_REGEXP) unset regexp_lib)
AC_CHECKING(if we have XawViewportSetCoordinates)
AC_CHECK_LIB(Xaw, XawViewportSetCoordinates, , AC_DEFINE(NO_VIEWPORT_SET_FUNCTIONS),-lXmu -lSM -lICE -lXext -lXt -lX11 -L/usr/X11/lib)

#
# Petal config
#
# We enable Petal only if
# - Tcl is installed
# - Tcl version is 7.5 or above
# - TclMIDI is installed

Petal=Petal

AC_CHECK_HEADER(tcl.h, TCL_CHECK=1, TCL_CHECK=0)

if test $TCL_CHECK -eq 1
then
     AC_PATH_PROG(TCLSH, tclsh)
     if test -n "$TCLSH"
     then
	# Check Tcl version:
	# 'package' feature was introduced in Tcl somewhere between 7.4 and 7.5
	# so we check for >=7.5

	AC_CHECKING(Tcl version)
	dnl Must double the [[]] here otherwise m4 wipes them out
	TCL_VERSION=`echo 'puts [[ info tclversion ]]' | $TCLSH`
	TCL_MAJOR=`echo $TCL_VERSION | cut -d. -f1`
	TCL_MINOR=`echo $TCL_VERSION | cut -d. -f2`

	if test \( $TCL_MAJOR -eq 7 -a $TCL_MINOR -ge 5 \) -o \( $TCL_MAJOR -gt 7 \)
	then
		# Check for TclMIDI
		AC_CHECKING(if TclMIDI is installed)
		TCL_MIDI_CHECK=`echo "package require tclmidi" | $TCLSH 2>&1`
		if test -n "$TCL_MIDI_CHECK"
		then
			Petal=""
			AC_MSG_WARN(TclMIDI is not installed (is \$TCLLIBPATH correctly set ?), Petal won't be built : -$TCL_MIDI_CHECK-)
		fi
		
	else
		Petal=""
		AC_MSG_WARN(Tcl version is too old : $TCL_VERSION. v7.5 or above is required, Petal won't be built)
	fi

     else
	Petal=""
	AC_MSG_WARN(Tcl seems to be installed but tclsh can't be found. Petal won't be built)

     fi

else
    Petal=""
    AC_MSG_WARN(Tcl is not installed. Petal won't be built)
fi


rosegarden=`pwd`
rosegarden_libs="Lists Yawn Interlock Midi Regexp"
unset makedepend_flag
unset extra_libs

sound_system="-DSYSTEM_SILENT"
PETAL_LD=$CC

case $host_os in
	*linux*)
	makedepend_flag="-D__i386__"
	defines="-DPOSIX_PLEASE"
	AC_CHECK_HEADER(sys/soundcard.h, sound_system="-DSYSTEM_OSS")
	rosegarden_libs="Lists Yawn Interlock Midi Regexp"
	# LDFLAGS="-L/usr/lib -L/usr/X11R6/lib"
	unset extra_libs

	PETAL_CFLAGS="-Wall -fPIC -g"
	PETAL_LDFLAGS="-shared -Wl,-Bstatic"
	;;

	*solaris*)
	defines="-DPOSIX_PLEASE"
	# LDFLAGS="-L/opt/SUNWspro/SC3.0/lib -L/usr/lib"
	extra_libs="-Bdynamic -ldl -Bstatic -lintl"

	if test $CC = "gcc"
	then	
		PETAL_CFLAGS="-fPIC"
		PETAL_LDFLAGS="-shared"

	else
		PETAL_CFLAGS="-KPIC"
		PETAL_LDFLAGS="-G"
	fi

	;;

	*freebsd*)
	defines="-DNO_SYS_ERRLIST"
	AC_CHECK_HEADER(sys/soundcard.h, sound_system="-DSYSTEM_FREEBSD")
	extra_libs="-lmalloc"
	# LDFLAGS="-L/usr/lib -L/usr/X11R6/lib"

	PETAL_LD=ld
	PETAL_CFLAGS="-fPIC"
	PETAL_LDFLAGS="-Bshareable"
	;;

	*irix*)
	defines="-DPOSIX_PLEASE"
	AC_CHECK_HEADER(dmedia/midi.h, sound_system="-DSYSTEM_SGI", sound_system="-DSYSTEM_ZILOG")
	extra_libs="-lmd -lmalloc"
	# LDFLAGS="-L/usr/lib"

	PETAL_CFLAGS=""
	if test $CC = "gcc"
	then
		PETAL_CFLAGS="-fPIC"
	fi
	PETAL_LDFLAGS="-shared"

	;;
esac

defines="$defines $sound_system"

#x_includes="-I$x_includes"
#x_libraries="-L$x_libraries"

AC_SUBST(rosegarden)

dnl AC_SUBST(x_includes)
dnl AC_SUBST(x_libraries)
AC_SUBST(X_CFLAGS)
AC_SUBST(X_EXTRA_LIBS)
AC_SUBST(X_PRE_LIBS)
AC_SUBST(X_LIBS)

AC_SUBST(editor_libs)
AC_SUBST(extra_libs)
AC_SUBST(rosegarden_libs)

AC_SUBST(makedepend_flag)
AC_SUBST(defines)
dnl AC_SUBST(regexp_lib)

AC_SUBST(Petal)
AC_SUBST(PETAL_LD)
AC_SUBST(PETAL_CFLAGS)
AC_SUBST(PETAL_LDFLAGS)

# Generate the Makefiles

AC_OUTPUT(Makefile petal/Makefile)

dnl AC_OUTPUT(Makefile editor/src/Makefile)

dnl AC_OUTPUT(topbox/src/Makefile yawn/src/Makefile lists/src/Makefile midi/src/Makefile editor/src/Makefile Makefile regexp/src/Makefile interlock/src/Makefile sequencer/src/Makefile)
